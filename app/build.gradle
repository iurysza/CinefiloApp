apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'realm-android'
apply plugin: 'me.tatarka.retrolambda'


android {
  compileSdkVersion 25
  buildToolsVersion rootProject.ext.supportLibrary
  defaultConfig {
    renderscriptTargetApi 19
    renderscriptSupportModeEnabled true
    applicationId "site.iurysouza.cinefilo"
    minSdkVersion 22
    targetSdkVersion 25
    versionCode 1
    versionName "1.0"

    /* replaced with custom MockTestRunner */
//     testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    testInstrumentationRunner "site.iurysouza.cinefilo.testhelpers.CineTestRunner"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  // Always show the result of every unit test, even if it passes.
  testOptions.unitTests.all {
    testLogging {
      events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
    }
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])


  compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibrary"
  compile "com.android.support:design:$rootProject.ext.supportLibrary"
  compile "com.android.support:cardview-v7:$rootProject.ext.supportLibrary"
  compile "com.android.support:recyclerview-v7:$rootProject.ext.supportLibrary"
  compile "com.android.support:palette-v7:$rootProject.ext.supportLibrary"

  //UI Libraries
  compile "com.wang.avi:library:$rootProject.ext.AviVersion"
  compile "com.github.armcha:SpaceNavigationView:$rootProject.ext.SpaceNavigationViewVersion"
  compile "com.malinskiy:superrecyclerview:$rootProject.ext.SuperRecyclerViewVersion"
  compile 'me.relex:circleindicator:1.2.2@aar'
  compile 'com.github.fafaldo:fab-toolbar:1.2.0'
  compile 'com.github.mmin18:realtimeblurview:1.1.0'
  compile 'com.github.medyo:fancybuttons:1.8.3'
  compile 'com.flaviofaria:kenburnsview:1.0.7'
  compile 'io.apptik.widget:multislider:1.3'
  compile 'de.hdodenhof:circleimageview:2.1.0'

  //Value Type Generator
  provided "com.google.auto.value:auto-value:1.2"
  apt "com.google.auto.value:auto-value:1.2"
  apt 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'

  //Lombok - Data types with annotations
  provided "org.projectlombok:lombok:1.12.6"
  apt "org.projectlombok:lombok:1.12.6"

  //FragmentStackManagement
  compile 'com.ncapdevi:frag-nav:1.2.5'

  //Date-Time
  compile "net.danlew:android.joda:$rootProject.ext.jodaTimeAndroid"

  //GreenRobot's Eventbus
  compile "org.greenrobot:eventbus:$rootProject.ext.EventBusVersion"

  //Dagger2
  apt 'com.google.dagger:dagger-compiler:2.7'
  compile 'com.google.dagger:dagger:2.7'
  provided 'javax.annotation:jsr250-api:1.0'

  //stetho
  debugCompile 'com.facebook.stetho:stetho:1.4.1'
  //realm pluggin
  debugCompile 'com.uphyca:stetho_realm:2.0.0'
  //Okhttp3 plugin
  debugCompile 'com.facebook.stetho:stetho-okhttp3:1.4.1'

  // realm adapters
  compile 'io.realm:android-adapters:1.3.0'
  // realm auto-generated __Fields
  compile 'dk.ilios:realmfieldnameshelper:1.0.0'

  //json parsing
  compile 'com.google.code.gson:gson:2.4'

  //reactive programming
  compile 'io.reactivex:rxjava:1.1.6'
  compile 'io.reactivex:rxandroid:1.2.1'
  compile 'com.jakewharton.rxbinding:rxbinding-support-v4:0.4.0'
  compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
  compile 'com.trello:rxlifecycle-components:1.0'
  compile 'com.trello:rxlifecycle:1.0'


  // retrofit
  compile 'com.squareup.retrofit2:retrofit:2.1.0'
  compile 'com.squareup.retrofit2:converter-gson:2.1.0'
  compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
  //okhttp
  compile 'com.squareup.okhttp3:okhttp:3.4.1'
  compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
  //picasso
  compile 'com.squareup.picasso:picasso:2.5.2'
  //timber
  compile 'com.jakewharton.timber:timber:4.1.2'
  //butterknife
  compile 'com.jakewharton:butterknife:8.1.0'
  apt 'com.jakewharton:butterknife-compiler:8.1.0'

  // Dependencies for local unit tests
  testCompile "junit:junit:$rootProject.ext.junitVersion"
  testCompile "org.mockito:mockito-all:$rootProject.ext.mockitoVersion"
  testCompile "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrestVersion"
  testCompile "org.powermock:powermock-module-junit4:$rootProject.ext.powerMockito"
  testCompile "org.powermock:powermock-api-mockito:$rootProject.ext.powerMockito"

  // Android Testing Support Library's runner and rules
  androidTestCompile "com.android.support.test:runner:$rootProject.ext.runnerVersion"
  androidTestCompile "com.android.support.test:rules:$rootProject.ext.rulesVersion"

  // Espresso UI Testing dependencies.
  androidTestCompile "com.android.support.test.espresso:espresso-core:$rootProject.ext.espressoVersion"
  androidTestCompile "com.android.support.test.espresso:espresso-contrib:$rootProject.ext.espressoVersion"
  androidTestCompile "com.android.support.test.espresso:espresso-intents:$rootProject.ext.espressoVersion"
  androidTestApt 'com.google.dagger:dagger-compiler:2.7'
  annotationProcessor 'com.google.dagger:dagger-compiler:2.7'
  androidTestCompile "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"
  androidTestCompile 'com.github.fabioCollini:DaggerMock:0.6.4'
  androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
  androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
}

/*
Resolves dependency versions across test and production APKs, specifically, transitive
dependencies. This is required since Espresso internally has a dependency on support-annotations.
*/
configurations.all {
  resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
  resolutionStrategy.force "com.google.guava:guava:$rootProject.ext.guavaVersion"
  resolutionStrategy.force "com.android.support:support-annotations:$rootProject.supportLibrary"
  resolutionStrategy.force "com.android.support:support-v4:$rootProject.supportLibrary"
}

/*
All direct/transitive dependencies shared between your test and production APKs need to be
excluded from the test APK! This is necessary because both APKs will contain the same classes. Not
excluding these dependencies from your test configuration will result in an dex pre-verifier error
at runtime. More info in this tools bug: (https://code.google.com/p/android/issues/detail?id=192497)
*/
configurations.compile.dependencies.each { compileDependency ->
  println "Excluding compile dependency: ${compileDependency.getName()}"
  configurations.androidTestCompile.dependencies.each { androidTestCompileDependency ->
    configurations.androidTestCompile.exclude module: "${compileDependency.getName()}"
  }
}
